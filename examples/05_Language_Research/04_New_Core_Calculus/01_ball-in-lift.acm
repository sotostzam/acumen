// Bouncing ball in lift program
//
// Authors: Eugenio Moggi and Adam Duracz

model Main(simulator) = 
initially 
  l  =  create Lift(0,2) 
always
  simulator.endTime + = 5

model Lift(w,a) =        // Speed,Acceleration of lift
initially
  h  =   0  , h'  =   w, // Position of lift
  w' =   a  ,            // Acceleration of lift
  x  =   5  , x'  =   0, // Position of ball
  v  =   0  , v'  = -10, // Speed of ball
  g  =  10  ,            // Gravity
  c  =   0.5             // Coefficient of restitution
always
  // Invariant
  claim x >= h ,
  // Flow
  h' = w , 
  w' = a ,
  x' = v ,
  if x <= h && v <= w && a > -g then
    v' = a
  else
    v' = -g,
  // Jumps
  if x <= h && v < w then
    v + = w - c * (v - w)
  noelse 
 
