/* 
   Works with semantic 2014
   Works with semantic 2013
   Works with semantic 2012
*/

// Day/Night Heater Hybrid Night example
//
// Auhor: Adam Duracz

model Main(simulator) =
  initially
    x = 10, x' = 10,
    time = 0, time' = 1,
    heating = "on", 
    period  = "day" 
  always
  time' = 1,
  match period with [
    "day" -> // Hybrid behaviour during the day
      if time > 12 then
        period+ = "night"
      noelse,
      match heating with [
        "on" ->
          if x > 23 then
            heating+ = "off"
          noelse,
          x' = 10
        |"off" ->
          if x < 19 then
            heating+ = "on"
          noelse,
          x' = -5-x
      ]
    |"night" ->
      if time > 24 then
        time+ = 0,
        period+ = "day" 
      noelse,
      match heating with [// Hybrid behaviour also at night
        "on" ->
          if x > 19 then
            heating+ = "off"
          noelse,
          x' = 10
        |"off" ->
          if x < 15 then
            heating+ = "on"
          noelse,
          x' = -5-x
      ]
  ],
  simulator.endTime+ = 48
