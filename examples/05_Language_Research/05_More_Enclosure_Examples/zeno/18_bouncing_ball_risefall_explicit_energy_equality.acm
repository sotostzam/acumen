model Main(simulator) =
  initially 
    mode = "Rise" & 
    x = 5 & x' = 0 & x'' = 0 &  
    r = 50 & r' = 0 
  always
  match mode with [
    "Rise" claim x >= 0 && x' >= 0 && 0 <= r && r == x'*x'/2 + 10*x -> 
      if x' == 0 then
        x+ == x &
        x'+ == x' &
        r+ == r &
        mode+ == "Fall" 
      noelse &
      x'' == -10 &
      r'  == 0 
    |"Fall" claim x >= 0 && x' <= 0 && 0 <= r && r == x'*x'/2 + 10*x -> 
      if x == 0 then
        x'+ == -0.5 * x' &
        r+ == [0.0..0.25]*r &
        mode+ == "Rise" 
      noelse &
      x'' == -10 &
      r'  == 0 
  ] &
  simulator.startTime+ == 0 &
  simulator.endTime+ == 3.5 &
  simulator.minSolverStep+ == 0.1 &
  simulator.minLocalizationStep+ == 0.01 &
  simulator.minComputationImprovement+ == 0.001   
