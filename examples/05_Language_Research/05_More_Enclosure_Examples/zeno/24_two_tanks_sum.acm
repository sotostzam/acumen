/* Two tanks
 * Authors: Michal Konecny and Jan Duracz
 * Implements the S2T model from the paper.
 */
model Main(simulator)  =
  initially 
    mode = "Fill1", 
    x1 = 1, x1' = 2,  
    x2 = 1, x2' = -3,
    x12 = 2, x12' = -1 
  always
  match mode with 
    ["Fill1" claim x1 >= 0&& x2 >= 0&& x12 >= 0&& x12 == x1 + x2 -> 
      if x2 == 0 then mode+ == "Fill2" noelse, 
      x1'  == 2, 
      x2'  == -3,
      x12' == -1 
    |"Fill2" claim x1 >= 0&& x2 >= 0&& x12 >= 0&& x12 == x1 + x2 -> 
      if x1 == 0 then mode+ == "Fill1" noelse, 
      x1'  == -2, 
      x2'  == 1,
      x12' == -1 ],
  simulator.startTime+ == 0,
  simulator.endTime+ == 2.5,
  simulator.minSolverStep+ == 2.5,
  simulator.minLocalizationStep+ == 0.001,
  simulator.minComputationImprovement+ == 1 
