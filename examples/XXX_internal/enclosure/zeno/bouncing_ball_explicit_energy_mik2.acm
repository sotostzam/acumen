/* Bouncing ball explicit energy mik2
 * Authors: Michal Konecny and Jan Duracz
 * Implements the EBB model from the paper.
 */
class Main(simulator)
  private 
    mode := "Fly"; 
    x1 := 5; x1' := 0; x1'' := 0;  
    r1 := 50; r1' := 0;
  end
  switch mode
    case "Fly"
    require x1 >= 0 && 0 <= r1 && r1 == x1'*x1'/2 + 10*x1
      if x1 == 0 && x1' <= 0
        x1' := -0.5*x1';
        r1 := [0.0..0.25]*r1;
        mode := "Fly";
      end;
      x1'' = -10;
      r1'  = 0;
  end;
  simulator.startTime := 0;
  simulator.endTime := 3.5;
  simulator.minSolverStep := 0.01;
  simulator.minLocalizationStep := 0.005;
  simulator.minComputationImprovement := 0.0005;
end
