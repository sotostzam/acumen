
// Bouncing Ball Overconstrained Improved Precision
//
// Authors:  Michal Konecny and Jan Duracz
// Note:     Run using Semantics -> Enclosure

class Main(simulator)
  private 
    x := 5; x' := 0; x'' := 0;  
    r := 50; r' := 0; // Helper variable added!
    mode := "Fly"
  end
  switch mode
    case "Fly" 
      claim x >= 0 && 
            r == x'*x'/2 + 10*x // Constraint added
      if x == 0 && x' <= 0
        x' := -0.5*x';
        r  := [0.0..0.25]*r; // Energy loss
        mode := "Fly"
      end;
      x'' = -10;
      r'  = 0 // Energy bound constant between events
  end;
  simulator.endTime := 3.5;
  simulator.minSolverStep := 0.01;
  simulator.minLocalizationStep := 0.01;
  simulator.minComputationImprovement := 0.001
end

// Note:  An additional invariant, based on a helper 
//        variable, makes the post-Zeno enclosure 
//        convergent.
//
// Note:  The helper variable r bounds the total
//        energy of the system in terms of x and x'. 
//        The constraint added based on r is used to 
//        express that energy is lost on each impact 
//        (event).
