
// Two Tanks Example
//
// Authors:  Michal Konecny and Jan Duracz
// Note:     Run using Semantics -> Enclosure

model Main(simulator)  =
  initially 
    x1 = 1, x1' = 2,  
    x2 = 1, x2' = -3,
    mode = "Fill1" 
  always
  match mode with [
    "Fill1" claim x1 >= 0&& x2 >= 0 -> 
      if x2 == 0 then mode+ = "Fill2" noelse, 
      x1' = 2, 
      x2' = -3
  | "Fill2" claim x1 >= 0&& x2 >= 0 -> 
      if x1 == 0 then mode+ = "Fill1" noelse, 
      x1' = -2, 
      x2' = 1
  ],
  simulator.startTime+ = 0,
  simulator.endTime+ = 2.5,
  simulator.minSolverStep+ = 2.5,
  simulator.minLocalizationStep+ = 0.001,
  simulator.minComputationImprovement+ = 0   

// Note:  For this system, invariants do not suffice 
//        to study the convergent post-Zeno dynamtics.
