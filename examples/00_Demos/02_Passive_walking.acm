// Passive Walking Example
// By Anita Santanna and Ryan Sinnet

model Main(simulator) = 
initially
   m = 2, d = 5, M = 10,g = 9.81, //model constants
   x1 = 0, y1 = 0, x2 = 0, y2 = 0,
   x3 = 0, y3 = 0, x4 = 0, y4 = 0,
   s = 0.035, //slope of the surface
   l = 0,
   xp = 0, yp = 0, xp' =0, yp' = 0,
   xp'' = 0, yp'' =0,
   H = (0,0), E = 0,
   t1 = 0 ,t1' = 0,t1'' = 0, //theta1 and theta2 angles
   t2 = 0,t2' = 2, t2'' = 0,t1p = 0, t2p = 0,
   q = (0,0.1), T = 0, V = 0, L = 0,
   h = 0, hd = 0,  //guards
   Qp = zeros(2,2), Qm = zeros(2,2),
   pm1x = 0 , pm1z = 0 , pM2x = 0 , pM2z = 0 , pm3x = 0 , pm3z = 0 , //position variable for animation
   sfx = 0,sfz = 0,p2x =0, p2z =0,x = 0, x' = 1,  //position of stance foot
   _3D = (),timer = 0, timer' = 0, _3DView = (0,0)
always
   simulator.endTime+ = 20, 
   m = 1, d = 1, M = 10, g = 9.81,
   l = 1, s = 0.035,
   xp'' = 0,yp''=0,
   q = (t1,t2),
   x1 = xp - 0.5*d*sin(t1), y1 = yp + 0.5*d*cos(t1),
   x2 = xp - d*sin(t1),     y2 = yp + d*cos(t1),
   x4 = x2 + d*sin(t2+t1), y4 = y2 - d*cos(t2+t1),
   x3 = x2 + 0.5*d*sin(t2+t1), y3 = y2 - 0.5*d*cos(t2+t1),
   V = g*(m*y1 + M*y2 + m*y3),
   T = 0.5*(m*((x1)'^2 + (y1)'^2) + M*((x2)'^2 + (y2)'^2) +m*((x3)'^2 + (y3)'^2)),  
   L = T - V,
   // Euler-Lagrangian equation of motion
   foreach i in 0 : (length(q) -1) do 
     L'[(q(i))']' - L'[q(i)]=0,

  //GUARD
  t1p = t1', t2p = t2',
  //h = x4*(sin(s)/cos(s))+y4,
  h = l*sin(s)*(sin(t1) + sin(t1+t2)),
  Qp= inv(((m*l*(l-0.5*l*cos(t2))+m*(0.5*l)^2 + M*l^2, m*0.5*l*(0.5*l-l*cos(t2))),
           (m*0.5*l^2*cos(t2), m*(0.5*l)^2))),
  Qm = ((-0.25*m*l^2+(M*l^2 + m*l^2)*cos(t2), -0.25*m*l^2), (0.25*m*l^2,0)),
  H =  trans((Qp * Qm) * trans((t1p,t2p))),
  if (h <= 0 &&  (h)' < 0 && (h)'' < 0) then
  //RESET MAP
  //reset for dynamics equation 
  t1+ =t1+t2,
  t2+ =-t2,
  t1' += H(0),
  t2' += H(1),
 //just for animation
 xp += x4,
 yp += yp - tan(s)*(d*sin(-t1) + d*sin(t2+t1)),
 timer + = 0
 noelse,
  //ADDITIONAL EQUATIONS FOR ANIMATION
  //position of link 1 - middle of stance leg
  pm1x = -(d * sin(t1)) / 2 , 
  pm1z = (d * cos(t1)) / 2 ,

  //position of link 2 - hip joint
  pM2x = -d * sin(t1) ,
  pM2z = d * cos(t1) ,

  //position of link 3 - middle of swing leg
  pm3x = (d*(-2*sin(t1) + sin(t1 + t2)))/2  ,
  pm3z = d*cos(t1) - (d*cos(t1 + t2))/2  ,

  //reference position of stance foot - just for animation
  p2x = d * (-sin(t1) + sin(t1 + t2)) , 
  p2z = 10*d * (cos(t1) - cos(t1 + t2)) ,
  // Camera movement
  x' = 0.4,
  _3DView = ((x,-10,0),(x,0,0)),
  timer' = 1,
  _3D=(Sphere center=(x1-2,0,y1) size=0.05 color=(1,0,0) ,
       Sphere center=(x2-2,0,y2) size=0.1 color=(1,0,0) ,
       Sphere center=(x3-2,0,y3) size=0.05 color=(0,1,0) ,
       Cylinder center=(x1-2,0,y1) radius= 0.01 length=d  color=(0,0,1) rotation=(3.1416/2,-t1,0),
       Cylinder center=(x3-2,0,y3) radius= 0.01 length=d  color=(0,0,1) rotation=(3.1416/2,-(t1+t2),0),
       Box center=(0-2,0,0) size=(100,0.01,2) color=(0.5,0.5,0) rotation=(3.1416/2,s,0))
 
 
   
         
    